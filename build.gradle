plugins {
    alias(libs.plugins.java)
    alias(libs.plugins.quarkus)
    alias(libs.plugins.maven.publish)
}

dependencies {
    // Use published BOM from Maven Local
    implementation platform('io.pipeline:pipeline-bom:1.0.0-SNAPSHOT')

    // Core Quarkus dependencies
    implementation 'io.quarkus:quarkus-grpc'
    implementation 'io.quarkus:quarkus-arc'
    implementation 'io.quarkus:quarkus-smallrye-health'
    implementation 'io.quarkus:quarkus-smallrye-stork'
    implementation 'io.quarkus:quarkus-opentelemetry'

    // REST API dependencies
    implementation 'io.quarkus:quarkus-rest'
    implementation 'io.quarkus:quarkus-rest-jackson'
    implementation 'io.quarkus:quarkus-smallrye-openapi'
    implementation 'io.quarkus:quarkus-micrometer-registry-prometheus'

    // Service Discovery dependencies for Consul
    implementation 'io.smallrye.stork:stork-service-discovery-consul'
    implementation 'io.smallrye.reactive:smallrye-mutiny-vertx-consul-client'

    // Security extensions
    implementation 'io.quarkus:quarkus-security'
    implementation 'io.quarkus:quarkus-elytron-security-properties-file'
    implementation 'io.quarkus:quarkus-elytron-security-oauth2'

    // SSL support
    implementation 'io.quarkus:quarkus-elytron-security'

    // Pipeline libraries (published to Maven Local)
    implementation 'io.pipeline:grpc-stubs:1.0.0-SNAPSHOT'
    implementation 'io.pipeline:pipeline-commons'
    implementation 'io.pipeline:pipeline-api'
    implementation 'io.pipeline:data-util'
    implementation 'io.pipeline:dynamic-grpc'
    implementation 'io.pipeline:dynamic-grpc-registration-clients'
    implementation 'io.pipeline:quarkus-pipeline-devservices:1.0.0-SNAPSHOT'

    // Test dependencies
    testImplementation 'io.quarkus:quarkus-junit5'
    testImplementation 'io.rest-assured:rest-assured'
    testImplementation 'org.assertj:assertj-core:3.27.0'
    testImplementation 'io.grpc:grpc-services'
    testImplementation 'org.testcontainers:testcontainers'
    testImplementation 'org.testcontainers:junit-jupiter'

    // Mockito for mocking in tests
    testImplementation 'org.mockito:mockito-core'
    testImplementation 'org.mockito:mockito-junit-jupiter'
    testImplementation 'io.quarkus:quarkus-junit5-mockito'
}

group = 'io.pipeline.module'
version = '1.0.0-SNAPSHOT'

java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

test {
    systemProperty "java.util.logging.manager", "org.jboss.logmanager.LogManager"
}

compileJava {
    options.encoding = 'UTF-8'
    options.compilerArgs << '-parameters'
}

compileTestJava {
    options.encoding = 'UTF-8'
}

// Publishing configuration
publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            artifactId = 'module-proxy'
            pom {
                name = 'Module Proxy'
                description = 'Proxy service for deploying pipeline modules in any language via gRPC'
                url = 'https://github.com/io-pipeline/module-proxy'

                licenses {
                    license {
                        name = 'Apache License 2.0'
                        url = 'https://www.apache.org/licenses/LICENSE-2.0'
                    }
                }

                developers {
                    developer {
                        id = 'krickert'
                        email = 'krickert@rokkon.com'
                        name = 'Pipeline Engine Team'
                    }
                }

                scm {
                    connection = 'scm:git:ssh://git@git.rokkon.com:2222/io-pipeline/module-proxy.git'
                    developerConnection = 'scm:git:ssh://git@git.rokkon.com:2222/io-pipeline/module-proxy.git'
                    url = 'https://git.rokkon.com/io-pipeline/module-proxy'
                }
            }
        }
    }

    repositories {
        // Publish to Maven Local for development
        mavenLocal()

        // Publish to Reposilite (primary artifact repository)
        maven {
            name = "Reposilite"
            url = uri(version.toString().endsWith('-SNAPSHOT')
                ? "https://maven.rokkon.com/snapshots"
                : "https://maven.rokkon.com/releases")
            credentials {
                username = project.findProperty("reposilite.user") ?: System.getenv("REPOS_USER") ?: "admin"
                password = project.findProperty("reposilite.token") ?: System.getenv("REPOS_PAT")
            }
        }

        // Publish to Gitea Maven registry (backup)
        maven {
            name = "Gitea"
            url = uri("https://git.rokkon.com/api/packages/io-pipeline/maven")
            credentials {
                username = project.findProperty("gitea.user") ?: System.getenv("GIT_USER") ?: "krickert"
                password = project.findProperty("gitea.token") ?: System.getenv("GIT_PAT")
            }
        }
    }
}